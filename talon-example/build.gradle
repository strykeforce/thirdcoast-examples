import jaci.openrio.gradle.GradleRIOPlugin
import jaci.openrio.gradle.frc.FRCJavaArtifact
import jaci.openrio.gradle.frc.RoboRIO

plugins {
    id 'java'
    id 'eclipse'
    id 'groovy'
    id 'jaci.openrio.gradle.GradleRIO' version '2018.03.06'
    id 'net.ltgt.apt-idea' version '0.15'
    id 'net.ltgt.errorprone' version "0.0.13"
}

def TEAM = 2767
def ROBOT_CLASS = 'org.example.Robot'

deploy {
    targets {
        target("roborio", RoboRIO) {
            team = TEAM
        }
    }
    artifacts {
        artifact('frcJava', FRCJavaArtifact) {
            targets << "roborio"
        }
    }
}

repositories {
    jcenter()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' } // nanohttpd
}

dependencies {
    compile wpilib()
    compile ctre()
    compile 'org.strykeforce.thirdcoast:core:18.5.0'
    compile 'com.google.dagger:dagger:2.14.1'
    compile 'ch.qos.logback:logback-classic:1.2.3'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.14.1'
//    annotationProcessor 'com.google.auto.factory:auto-factory:1.0-beta5'
    compileOnly 'org.jetbrains:annotations:15.0'
    compileOnly 'com.google.code.findbugs:jsr305:3.0.2'
}

jar {
    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    manifest GradleRIOPlugin.javaManifest(ROBOT_CLASS)
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.4.1'
}
